%macro MakeMHDxFile(population, startdate, enddate, outds);
*from MHRN_querydata_2012 but adding two more classes - conduct disorder and adjustment reaction
and limiting to population of interest;

*** use RJ syntax from AFSP, easier to read;
%let depr = '2962', '2963', '29682', '2980', '3004', '3090', '30112',  '3091', '30928' '311' ;
%let bipolar = '2960','2961', '2964', '2965', '2966', '2967', '29680', '2968', '30111', '30113';
%let anx = '3000', '3002', '3003', '3092', '30921', '30924', '30981' ;
%let adjust = '308', '309';
%let psychosis = '2971', '2973', '2988', '2989', '30122';
%let dementia = '290', '78093';
%let subs_abuse = '291', '292', '303', '304', '305';
%let ADD = '314';
%let ASD = '299';
%let schizophrenia = '295';
%let PD = '301';
%let ED = '3071','3075';
%let selfharm = 'E95','E98';
%let injury= '87','88','89','96','97','98','9947';
*following per Brian Ahmedani for possible suicide attempt with subset of injuries;
%let sui_risk = '2908','2909','29383','295','296','297','298','299','3004','301','309','311','7801';
%let conduct = '312', '31381';
**exclusions:
*remission and tobacco;
%let subs_exclude = '29285','29269','30393','30303','30403','30413','30423','30433','30443','30453','30463','30473','30483','30493','30503','3051','30523','30533','30543','30553','30563','30573','30583','30593';
*late effects, 307 NOT eating disorder;
%let other_exclude = 'E959','E989','3070','30752','30753','30754';

proc sql ;
  create table worklib.Alldx as
  select *, compress(dx, '.') as dx_comp
  from &_vdw_dx  
  where  adate between &startdate and &enddate  
  	and enctype not in ('LO', 'RO')
	and mrn in (SELECT mrn from &population)
  order by 1, 2
  ;
quit;

data worklib.dx_raw;
	set worklib.Alldx ;
	length dx_class $40.;
	  if( 
		dx_comp in: (&depr.) 
		or dx_comp in:(&bipolar.)
		or dx_comp in:(&anx.)
		or dx_comp in:(&adjust.)
		or dx_comp in:(&ADD.)
		or dx_comp in:(&ASD.)
		or dx_comp in:(&schizophrenia.)
		or dx_comp in:(&psychosis.)
		or dx_comp in:(&dementia.)
		or dx_comp in:(&sui_risk.)
		or dx_comp in: (&subs_abuse.)
		or dx_comp in:(&dementia.)
		or dx_comp in:(&PD.)
		or dx_comp in:(&ED.)
		or dx_comp in:(&selfharm.)
		or dx_comp in:(&conduct.)
		);
	if dx_comp in: (&subs_exclude.) then delete;
	if dx_comp in: (&other_exclude.) then delete;
	if dx_comp in: (&subs_abuse.) 	then dx_class = 'Substance Abuse Disorder'; 
	if dx_comp in: (&depr.) then dx_class = 'Depressive Disorder'; 
	else if  dx_comp in:(&bipolar.) then dx_class = 'Bipolar Spectrum Disorder';
	else if dx_comp in:(&anx.)then dx_class = 'Anxiety Disorder';
	else if dx_comp in:(&adjust.)then dx_class = 'Adjustment Reaction';
	else if  dx_comp in:(&ADD.) then dx_class = 'Attention Deficit Disorder';
	else if dx_comp in:(&ASD.) then dx_class = 'Autism Spectrum Disorder';
	else if  dx_comp in:(&schizophrenia.) then dx_class = 'Schizophrenia Spectrum Disorder';
	else if  dx_comp in:(&dementia.) then dx_class = 'Dementia';
	else if dx_comp in:(&psychosis.) then dx_class = 'Other Psychosis';
	else if dx_comp in: (&subs_abuse.) then dx_class = 'Substance Abuse Disorder';
	else if dx_comp in:(&PD.) then dx_class = 'Personality Disorder'; 
	else if dx_comp in:(&ED.) then dx_class = 'Eating Disorder'; 
	else if dx_comp in:('E95') then dx_class = 'Self-Harm';
	else if dx_comp in:('E98') then dx_class = 'Possible Self-Harm';
	else if dx_comp in:(&conduct.) then dx_class = 'Conduct Disorder';
	else delete;
run;

data sui_idea_assoc;
	set worklib.Alldx ;
	length dx_class $40.;
	if (dx_comp in: (&sui_risk.)
		or dx_comp in: ('V6284'));
	if dx_comp in:  ('V6284') then dx_class = 'Suicide ideation';
	else dx_class = 'Suicide-associated mhdx';
run;
proc freq data=sui_idea_assoc;
	WHERE DX_CLASS = 'Suicide-associated mhdx';
	tables dx_comp;
run;
data injury;
	set worklib.Alldx ;
	length dx_class $40.;
	if dx_comp in: (&injury.);
	dx_class = 'Suicide-associated injury';
run;
proc sql;
	create table combo_attempts as
	select  m.*, i.dx_comp as otherdx, 'Injury/poisoning with suicide ideation' as attempt_class
	from sui_idea_assoc m inner join injury i 
		on m.mrn =i.mrn and m.adate = i.adate
	where m.dx_class ='Suicide ideation'
	UNION 
	select a.*, b.dx_comp as otherdx, 'Injury/poisoning with MH diagnosis' as attempt_class
	from sui_idea_assoc a inner join injury b 
		on a.mrn =b.mrn and a.adate = b.adate
	where a.dx_class ='Suicide-associated mhdx'
	;
quit;

proc freq data=combo_attempts;
	FORMAT DX_COMP $3. OTHERDX $2.;
	tables dx_comp*otherdx / norow nocol nopercent;
run;

Data &outds;
	set worklib.dx_raw combo_attempts;
	if missing(attempt_class) = 0 then dx_class =attempt_class;
run;
proc freq data=&outds;
	tables dx_class;
run;
proc datasets;
	delete sui_idea_assoc; delete injury; delete combo_attempts;
run;

%mend;